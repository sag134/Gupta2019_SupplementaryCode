#Model of NFkB signaling containing simplifed versions of reactions from either Pekalski et al. 2013, or Lee et al. 2014
#All the parameters are fitted, so the parameter values here are 

begin model
begin molecule types
TNFR(st~a~i) # active/inactive TNFR1 receptors 
TNF()
ikk(s~N~A) #IKK is active/neutral
ikba(loc~c~n,nfkb) # ikba is found in the cytoplasm/nucleus, and has a binding site for nfkb
nfkb(loc~c~n,ikba) # nfkb is found in the cytoplasm/nucleus, and has a binding site for IkB
A20()
end molecule types

begin parameters

#proteinparameters
Tot_NFKB 6.5546 #Total protein concentration of NFkB, molecules
IKK_N 4.7454 #Total protein concentration of IKK, molecules
R      2.5258 #Total protein concentration or receptor, molecules

# Parameters for reactions that were adapted from Pekalski et al. 2013
#c_deg         log10(2e-4)   # we don't consider TNF degradation
k_b         0.0352 # TNF-TNFR activation rate, per molecule per second
k_f         -3.0688 #TNFR inactivation rate, per second
k_a           -4.5164 #TNFR mediated IKK activation rate, per molecule per second
k_4          -7.4173 #IKK inactivation rate, per second
# Parameters for reactions that were adapted from Lee et al. 2014
ka1a = -7 #IkB-NFkB binding rate, per molecule per second
kd1a = -8 # IkB-NFkB unbinding rate, per second
ki1 = -2.057 # NFkB entry into the nucleus, per second
ke1 = -5.9334 # NFkB exit from the nucleus, per second
ki2 = -4.1089 #IkB entry into the nucleus, per second
ke2 = -10.6289 #IkB exit from the nucleus, per second
ke2a = -3.5859 #IkB-NFkB complex exit from nuclues, per second
c4a = -4.6363 #Basal degradation of free cytoplasmic IkB, per second
c5a = -4.6 #Basal degradation of bound cytoplasmic IkB, per second
kt1a = -7.3178 ##IKK mediated degradation of free cytoplasmic IkB, per molecule second
kt2a = -8 #IKK mediated degradation of bound cytoplasmic IkB, per molecule per second
c1a = -3.2082 #NFkB mediated production of free cytoplasmic IkB
c_3 = -20
c_1 = -20
a20_ikk_inact =-20
a20_tnfr_inact = -20
#Group lasso regularization parameters
A20_par 0
Activation_par 0
ikb_par 0
end parameters

begin observables
Molecules nNFKB nfkb(loc~n,ikba) #nuclear free nfkb
Molecules cNFKB nfkb(loc~c,ikba) #cytoplasmic free nfkb
Molecules cikba ikba(loc~c,nfkb) #cytoplasmic free ikba
Species Ccomplex nfkb(loc~c,ikba!1).ikba(loc~c,nfkb!1) #cytoplasmic ikba-nfkb complex
Molecules nikba ikba(loc~n,nfkb) #nuclear free ikba
Species ncomplex nfkb(loc~n,ikba!1).ikba(loc~n,nfkb!1) #nuclear ikba-nfkb complex
Molecules ikkN ikk(s~N) #neutral ikk
Molecules ikka ikk(s~A) #activated ikk
Molecules TNFR_a TNFR(st~a) #active TNFR
Molecules TNFR_i TNFR(st~i) #inactive TNFR
Molecules A20 A20()
Molecules TNF TNF() #TNF
end observables

begin seed species
nfkb(loc~n,ikba) 0 #nuclear free nfkb
nfkb(loc~c,ikba) 0 #cytoplasmic free nfkb
ikba(loc~c,nfkb) 0#cytoplasmic free ikba
nfkb(loc~c,ikba!1).ikba(loc~c,nfkb!1)   10^Tot_NFKB   #cytoplasmic ikba-nfkb complex
ikba(loc~n,nfkb) 0 #nuclear free ikba
nfkb(loc~n,ikba!1).ikba(loc~n,nfkb!1) 0 #nuclear ikba-nfkb complex
ikk(s~N) 10^IKK_N #neutral ikk
ikk(s~A) 0 #activated ikk
TNFR(st~a)           0 #active TNFR
TNFR(st~i)           10^R #inactive TNFR
A20() 0
TNF() 			0 #TNF
end seed species

begin reaction rules
R1_TNF_degradation: TNF()               ->  0                 0# 2e-4 #per second TNF degradation rate constant from Pekalski et al. (Lipniacki)
R2_TNFR_activation: TNFR(st~i) + TNF()  ->  TNFR(st~a)  + TNF() (10^(k_b+Activation_par))
R3_TNFR_inactivation: TNFR(st~a)               ->  TNFR(st~i)               (10^(k_f+Activation_par)) 
R4_ikk_activation: ikk(s~N)  +TNFR(st~a)              ->  ikk(s~A)+TNFR(st~a)     (10^(k_a+Activation_par))
R5_ikk_inactivation: ikk(s~A)                ->  ikk(s~N)              10^(k_4+Activation_par)    
    
R6_reversible_cytoplasmic_complex_formation: ikba(loc~c,nfkb) + nfkb(loc~c,ikba) <-> ikba(loc~c,nfkb!1).nfkb(loc~c,ikba!1) 10^(ka1a+ikb_par),10^(kd1a+ikb_par)
R7_reversible_nuclear_complex_formation: nfkb(loc~n,ikba) + ikba(loc~n,nfkb) <-> nfkb(loc~n,ikba!1).ikba(loc~n,nfkb!1) 3.3*10^(ka1a+ikb_par),10^(kd1a+ikb_par)

R8_ikk_mediated_cytoplasmic_free_ikb_degradation: ikk(s~A)+ikba(loc~c,nfkb) -> ikk(s~A) 10^(kt1a+Activation_par)
R9_ikk_mediated_cytoplasmic_bound_ikb_degradation: ikk(s~A)+nfkb(loc~c,ikba!1).ikba(loc~c,nfkb!1) ->ikk(s~A)+ nfkb(loc~c,ikba) 10^(kt2a+Activation_par)

R10_basal_cytoplasmic_free_ikb_degradation: ikba(loc~c,nfkb) -> 0 10^(c4a+ikb_par)
R11_basal_cytoplasmic_bound_ikb_degradation: nfkb(loc~c,ikba!1).ikba(loc~c,nfkb!1) -> nfkb(loc~c,ikba) 10^(c5a+ikb_par) 

R12_nfkb_shuttling: nfkb(loc~c,ikba) <-> nfkb(loc~n,ikba) (10^(ki1+Activation_par)),(10^(ke1+Activation_par)) 
R13_ikb_shuttling: ikba(loc~c,nfkb) <-> ikba(loc~n,nfkb) (10^(ki2+ikb_par)),(10^(ke2+ikb_par)) 

R14_ikb_linear_transcription: nfkb(loc~n,ikba) -> nfkb(loc~n,ikba) + ikba(loc~c,nfkb) 10^(c1a+ikb_par)
R15_export_nuclearcomplex: ikba(loc~n,nfkb!1).nfkb(loc~n,ikba!1) -> ikba(loc~c,nfkb!1).nfkb(loc~c,ikba!1) 10^(ke2a+ikb_par)

R16_A20_linear_transcription:  nfkb(loc~n,ikba) -> nfkb(loc~n,ikba) + A20() 10^(c_1+A20_par)
R17_A20_mediated_ikk_inactivation: A20() + ikk(s~A) -> A20() + ikk(s~N) 10^(a20_ikk_inact+A20_par)
R18_A20_mediated_tnfr_inactivation: A20() +  TNFR(st~a)  -> A20() + TNFR(st~i) 10^(a20_tnfr_inact+A20_par)
R19_A20_degradation: A20() -> 0 10^(A20_par+c_3)
end reaction rules

end model


generate_network({overwrite=>1})
writeMfile({})
writeMexfile({})
#Equilibration
simulate_ode({t_start=>0,t_end=>1e8,n_steps=>1000,atol=>1.0E-6,rtol=>1.0E-8})
#Simulate pulse
setConcentration("TNF()",(5))
simulate_ode({t_start=>0,t_end=>300,n_steps=>300,atol=>1.0E-6,rtol=>1.0E-8})
#Simulate washout
setConcentration("TNF()",0)
simulate_ode({t_start=>0,t_end=>14100,n_steps=>47,atol=>1.0E-6,rtol=>1.0E-8})